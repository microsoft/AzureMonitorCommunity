// Author: Evgeny Ternovsky, Rafi Rabo
// Display name: Top 10 users by table access
// Description: Lists top 10 users by the distinct count of tables that were accessed.
// Categories: Audit,Azure Monitor
// Resource types: Log Analytics workspaces
// Topic: Usage

let timerange = 7d; //replace with time period over which to look
// List of tables that were used
let validTables=(
    Usage // Consider changing to workspace("MyLogAnalyticsWS").Usage , replace with the name of the workspace for which the metric is to be evaluated
    | where TimeGenerated > ago(timerange)
    | distinct DataType
);
LAQueryLogs
| where TimeGenerated > ago(timerange)
| where QueryText has_any (validTables)
| extend queryLine = split(QueryText, "\n")
| mv-expand queryLine
| extend dummy=1
| join kind=inner (validTables | extend dummy=1) on dummy
| where queryLine has DataType
| summarize dcount(DataType) by AADEmail
| top 10 by dcount_DataType desc