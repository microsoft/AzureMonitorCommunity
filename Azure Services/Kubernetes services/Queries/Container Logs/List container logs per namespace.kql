// Author: Microsoft Azure
// Display name: List container logs per namespace
// Description: View container logs from all the namespaces in the cluster.
// Categories: Containers
// Resource types: Kubernetes services
// Solutions: ContainerInsights
// Topic: Container Logs


let notBefore=ago(1h);
ContainerLog
| where TimeGenerated > notBefore
|join kind=leftouter (
KubePodInventory
| distinct Computer, ClusterId, ServiceName, ControllerName, ContainerID, ContainerName, Name, Namespace, ClusterName
| project-rename PodName=Name
)//KubePodInventory Contains pod metadata
on ContainerID
| extend ContainerName = split(ContainerName, "/")[1] // KubePodInventory stores ContainerName in format PodUI/container name, hence we split
| sort by TimeGenerated
| project TimeGenerated, Namespace, PodName, ContainerName, LogEntry, LogEntrySource
